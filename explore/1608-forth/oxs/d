\ load development code
\ needs l

reset

: blink ( -- )  \ blink the on-board LED until a key is pressed
  OMODE-PP LED1 io-mode!  begin  LED1 iox!  100 ms  key? until ;

\ print out received packet info until a key is pressed
\ format: #bytes b3..b0 b7..b4 rssi lna afc
: r69try ( -- )
\ 42 8686 rf-init
  begin
    rf-recv  ?dup if
      cr . rf.buf @ hex. rf.buf 4 + @ hex. rf.rssi @ . rf.lna @ . rf.afc @ .
    then
  key? until ;

: adctry ( -- )  +adc  begin  PA0 adc .  key? until ;
: uartry ( -- )  +uart uart. ;
: pwmtry ( -- )  1 LED2 +pwm 9900 LED2 pwm ;  \ inverted logic
: t69try ( n - ) 30 rf-power  0 <# #s #> 0 rf-send ;

config-pins
quad-adc
count-pulses
6 8686 rf-init

\ -----------------------------------------------------------------------------

0 0 2variable counts1  \ previous pulses1 state
0 0 2variable counts2  \ previous pulses2 state
0 0 2variable counts3  \ previous pulses3 state

                       2000 constant pulses/kwh
                3600 1000 * constant ms/hour
ms/hour 1000 pulses/kwh u*/ constant pulse-factor  \ 1800000

: pulse-to-watt ( count ms -- watt )
  dup if pulse-factor -rot u*/ else nip then  \ avoid divide by zero
;

: pulse-diff ( pa ca -- count ms )
  >r  dint 2@ eint  \ lock out interrupts during fetch
  over r@ @ = if
    rdrop 2drop 0 0
  else
    \ TODO horrible stack juggling, need to simplify
    2dup r@ 2@ rot swap - -rot - 2swap r> 2!
  then ;

: x1 ( -- )
  cr
  pulses1 counts1 pulse-diff pulse-to-watt
  ." P1: " dup if counts1 @ . then .
  pulses2 counts2 pulse-diff pulse-to-watt
  ." P2: " dup if counts2 @ . then .
  pulses3 counts3 pulse-diff pulse-to-watt
  ." P3: " dup if counts3 @ . then .
;

: x2 ( -- )
  \ FIXME yuck, all items end up getting inserted in reversed order
  <v
  pulses3 counts3 pulse-diff pulse-to-watt dup u14#v if counts3 @ u14#v then
  pulses2 counts2 pulse-diff pulse-to-watt dup u14#v if counts2 @ u14#v then
  pulses1 counts1 pulse-diff pulse-to-watt dup u14#v if counts1 @ u14#v then
  1 u#v
  v> 0 rf-send
;

: y ( -- ) begin 10000 ms x2 key? until ;

\ -----------------------------------------------------------------------------

    800 constant #capt
#capt 2* buffer: capt

: dma-sync ( -- )  \ synchronise to the ADC half-transfer flag in the DMA unit
  DMA1-ISR @ DMA1-IFCR !
  begin DMA1-ISR @ 2 bit and until  \ wait for half-transfer on channel 0
;

: acapture ( chan -- )  \ sync, copy, and print samples from a separate buffer
  dma-sync
  #capt 0 do i 4 * over + 2* adata + h@ i 2* capt + h! loop
  #capt 0 do i 2* capt + h@ . loop drop ;

\ 0 acapture

\ -----------------------------------------------------------------------------
\ vim: set ft=forth :
