\ load development code
\ needs l

reset

: blink ( -- )  \ blink the on-board LED until a key is pressed
  OMODE-PP LED1 io-mode!
  begin
    LED1 iox!
    1000000 0 do loop
  key? until ;

: tftry tft-init clear TFT-BL ios! demo ;

: pwmtry ( -- )  7200 LED2 +pwm 100 LED2 pwm ;

\ -----------------------------------------------------------------------------

\ located above the 20K RAM area used by Mecrisp's std build for STM32F103's
$20008000 602 2constant adc-buffer  \ 301 2-byte ints in range 0..4095

: pp ( x y )  10 + swap 20 + swap putpixel ;

: border
  $0888 tft-fg !
  202 -1 do  i -1 pp  i 301 pp  loop
  302 -1 do  -1 i pp  201 i pp  loop
  $FFFF tft-fg ! ;

: grid
  $0888 tft-fg !
  225 0 do
    305   0 do  j i pp  5 +loop          \ horizontal grid lines
    i 148 pp  i 152 pp                   \ major tick marks on Y axis
  25 +loop
  205 0 do  i 149 pp  i 151 pp  5 +loop  \ minor tick marks on Y axis
  325 0 do
    205  0 do  i j pp  5 +loop           \ vertical grid lines
    98 i pp 102 i pp                     \ major tick marks on X axis
  25 +loop
  305 0 do  99 i pp 101 i pp  5 +loop    \ minor tick marks on X axis
  $FFFF tft-fg ! ;

602 buffer: trace

: scope ( -- )  \ very crude continuous ADC capture w/ graph plot
  1000 systick-hz
  TFT-BL ioc!
  tft-init
  ticks @ clear  ticks @ swap - .  \ ms to clear the screen
  ticks @ border ticks @ swap - .  \ ms to draw the border
  ticks @ grid   ticks @ swap - .  \ ms to draw the grid
  TFT-BL ios!

  11 dac1-awg
  adc-buffer PB0 162 adc1-dma

  begin
\   ticks @
    \ grab and draw the trace
    301 0 do
      adc-buffer drop i 2* + h@ 20 / 1+
      dup trace i 2* + h!  \ also save a copy in a buffer
      i pp
    loop
    40 ms  \ leave the trace on the screen for a while
    \ bail out on key press, with the trace stil showing
    key? 0= while
    \ clear the trace again
    tft-bg @ tft-fg !
    301 0 do
      trace i 2* + h@
      i pp
    loop
    $FFFF tft-fg !
    grid  \ redraw the grid
\   ticks @ swap - .
  repeat ;

\ -----------------------------------------------------------------------------

: sleep ( -- ) [ $BF30 h, ] inline ;  \ WFI Opcode, enters sleep mode

$40007000 constant PWR
      PWR $0 + constant PWR-CR
      PWR $4 + constant PWR-CSR
$E000ED00 constant SCB
     SCB $14 + constant SCB-SCR

: standby ( -- )  \ go into low-power standby mode
  2 bit PWR-CR bis!  \ CWUF
  1 bit PWR-CR bis!  \ PDDS
  2 bit SCB-SCR bis!  \ SLEEPDEEP
  sleep ;

\ TODO not working, not even with OMODE-AF-OD PA0 io-mode!
: ena-wup ( -- )  \ enable wakeup pin
  8 bit PWR-CSR bis! ;

\ -----------------------------------------------------------------------------

\ 10 %pwm
\ vim: set ft=forth :
