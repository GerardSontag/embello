\ load development code
\ needs l

reset

: blink ( -- )  \ blink the on-board LED until a key is pressed
  OMODE-PP LED1 io-mode!
  begin
    LED1 iox!
    1000000 0 do loop
  key? until ;

: ledon led2 ios! ;
: ledoff led2 ioc! ;
: busy ( u -- ) 100 * 0 ?do loop ;
: %pwm ( pct -- )
  begin
    ledon  dup busy
    ledoff  100 over - busy
  key? until ;

: tftry tft-init clear TFT-BL ios! demo ;

\ -----------------------------------------------------------------------------

\ located above the 20K RAM area used by Mecrisp's std build for STM32F103's
$20008000 600 2constant adc-buffer  \ 300 2-byte ints in range 0..4095

: pp ( x y )  10 + swap 20 + swap putpixel ;

: border
  $0888 tft-fg !
  201 0 do  i 0 pp  i 300 pp  loop
  301 0 do  0 i pp  200 i pp  loop
  $FFFF tft-fg ! ;

: grid
  $0888 tft-fg !
  200 25 do
    305   0 do  j i pp  5 +loop
    153 148 do  j i pp     loop
  25 +loop
  205 0 do
      3   1 do  j i pp  loop
    152 149 do  j i pp  loop
    300 298 do  j i pp  loop
  5 +loop
  300 25 do
    205 0 do  i j pp  5 +loop
    103 98 do  i j pp  loop
  25 +loop
  305 0 do
      3   1 do  i j pp  loop
    102  99 do  i j pp  loop
    200 198 do  i j pp  loop
  5 +loop
  $FFFF tft-fg ! ;

600 buffer: trace

: scope ( -- )  \ very crude continuous ADC capture w/ graph plot
  1000 systick-hz
  TFT-BL ioc!
  tft-init
  ticks @ clear ticks @ swap - .
  ticks @ border ticks @ swap - .
  ticks @ grid ticks @ swap - .
  TFT-BL ios!

  11 dac1-awg
  adc-buffer PB0 501 ( 58000 ) adc1-dma

  begin
\   ticks @
    \ grab and draw the trace
    300 1 do
      adc-buffer drop i 2* + h@ 20 / 1+
      dup trace i 2* + h!  \ also save a copy in a buffer
      i pp
    loop
    40 ms  \ leave the trace on the screen for a while
    \ bail out on key press, with the trace stil showing
    key? 0= while
    \ now clear the trace and redraw the grid
    $0000 tft-fg !
    300 1 do
      trace i 2* + h@
      i pp
    loop
    $FFFF tft-fg !
    grid
\   ticks @ swap - .
  repeat ;

\ -----------------------------------------------------------------------------

: sleep ( -- ) [ $BF30 h, ] inline ;  \ WFI Opcode, enters sleep mode

$40007000 constant PWR
      PWR $0 + constant PWR-CR
      PWR $4 + constant PWR-CSR
$E000ED00 constant SCB
     SCB $14 + constant SCB-SCR

: standby ( -- )  \ go into low-power standby mode
  2 bit PWR-CR bis!  \ CWUF
  1 bit PWR-CR bis!  \ PDDS
  2 bit SCB-SCR bis!  \ SLEEPDEEP
  sleep ;

\ TODO not working, not even with OMODE-AF-OD PA0 io-mode!
: ena-wup ( -- )  \ enable wakeup pin
  8 bit PWR-CSR bis! ;

\ -----------------------------------------------------------------------------

\ 10 %pwm
\ vim: set ft=forth :
