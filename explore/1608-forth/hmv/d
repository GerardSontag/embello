\ load development code
\ needs l

reset

: blink ( -- )  \ blink the on-board LED until a key is pressed
  OMODE-PP LED1 io-mode!
  begin
    LED1 iox!
    1000000 0 do loop
  key? until ;

: ledon led2 ios! ;
: ledoff led2 ioc! ;
: busy ( u -- ) 100 * 0 ?do loop ;
: %pwm ( pct -- )
  begin
    ledon  dup busy
    ledoff  100 over - busy
  key? until ;

: tftry tft-init clear TFT-BL ios! demo ;

\ -----------------------------------------------------------------------------

\ located above the 20K RAM area used by Mecrisp's std build for STM32F103's
$20008000 1200 2constant adc-buffer  \ 600 2-byte ints in range 0..4095

: grid
  $0888 tft-fg !
  200 25 do
    305   0 do  j i putpixel  5 +loop
    153 148 do  j i putpixel     loop
  25 +loop
  205 0 do
      3   1 do  j i putpixel  loop
    152 149 do  j i putpixel  loop
    300 298 do  j i putpixel  loop
  5 +loop
  201 0 do
    i 0 putpixel
    i 300 putpixel
  loop
  300 25 do
    205 0 do  i j putpixel  5 +loop
    103 98 do  i j putpixel  loop
  25 +loop
  305 0 do
      3   1 do  i j putpixel  loop
    102  99 do  i j putpixel  loop
    200 198 do  i j putpixel  loop
  5 +loop
  301 0 do
    0 i putpixel
    200 i putpixel
  loop
  $FFFF tft-fg !
;

: scope ( -- )  \ very crude continuous ADC capture w/ graph plot
  tft-init clear grid TFT-BL ios!

  100 dac1-awg
  adc-buffer PB0 4200 adc1-dma

  begin clear grid
    300 0 do
      adc-buffer drop i 2* + h@ 20 / i putpixel
    loop
\   500 ms
  key? until ;

\ -----------------------------------------------------------------------------

: sleep ( -- ) [ $BF30 h, ] inline ;  \ WFI Opcode, enters sleep mode

$40007000 constant PWR
      PWR $0 + constant PWR-CR
      PWR $4 + constant PWR-CSR
$E000ED00 constant SCB
     SCB $14 + constant SCB-SCR

: standby ( -- )  \ go into low-power standby mode
  2 bit PWR-CR bis!  \ CWUF
  1 bit PWR-CR bis!  \ PDDS
  2 bit SCB-SCR bis!  \ SLEEPDEEP
  sleep ;

\ TODO not working, not even with OMODE-AF-OD PA0 io-mode!
: ena-wup ( -- )  \ enable wakeup pin
  8 bit PWR-CSR bis! ;

\ -----------------------------------------------------------------------------

\ 10 %pwm
\ vim: set ft=forth :
