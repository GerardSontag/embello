title Reformat A drive in RAM to 360K -jcw, 2017-03-19

; Usage: fmt360

WSTART	equ	0000h
BDOS	equ	0005h

SELDSK  equ     14
DELF	equ	19
GETADR  equ     31
RSTDRV  equ     37

	org 100h

        ld e,0          ; select drive A:
        ld c,SELDSK
        call BDOS

        ld de,fcb
        ld c,DELF       ; delete all files
        call BDOS

        ld c,GETADR     ; get DPB of A:
        call BDOS

        ex de,hl        ; overwrite with our version
        ld hl,dpb
        ld bc,dpblen
        ldir

        ld de,1         ; reset drive A:
        ld c,RSTDRV
        call BDOS

        jp WSTART

dpb:	dw 26  ; SPT - sectors per track
	db 4   ; BSH - block shift factor
	db 15  ; BLM - block mask
	db 1   ; EXM - Extent mask
	dw 175 ; DSM - Storage size (blocks - 1)
	dw 63  ; DRM - Number of directory entries - 1
	db 128 ; AL0 - 1 bit set per directory block
	db 0   ; AL1 - ... 8 more bits
	dw 0   ; CKS - DIR check vector size (DRM+1)/4 (0=fixed disk)
	dw 2   ; OFF - Reserved tracks
dpblen  equ $-dpb

fcb:    db 0,"????????","???"
        ds 20

        end
