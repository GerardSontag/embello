\ load development code
\ needs l

reset

: blink ( -- )  \ blink the on-board LED until a key is pressed
  OMODE-PP LED io-mode!  begin  LED iox!  100 ms  key? until ;

\ print out received packet info until a key is pressed
\ format: #bytes b3..b0 b7..b4 rssi lna afc
: r69try ( -- )
  42 8686 rf-init
  begin
    rf-recv  ?dup if
      cr . rf.buf @ hex. rf.buf 4 + @ hex. rf.rssi @ . rf.lna @ . rf.afc @ .
    then
  key? until ;

: lcdtry ( -- ) lcd-init show-logo  1000 ms  lcd-init demo ;
: pwmtry ( -- ) 1 LED +pwm 100 LED pwm ;
: t69try ( n - ) 15 rf-power  0 <# #s #> 0 rf-send ;

\ -----------------------------------------------------------------------------

PA2 constant HEATER-SENSE
PA3 constant POWER-SENSE
PA8 constant TEMP-POWER
PB0 constant TEMP-SENSE
PB3 constant HEATER
PB4 constant BLACK-BTN
PB5 constant WHITE-BTN

: splash ( -- )
  clear
  64 32 58 28 ellipse
  64 32 60 30 ellipse
  s" Pico Reflow" 21 20 drawstring
  s" Controller" 25 30 drawstring
  s" v0.1" 48 47 drawstring
  20 42 108 42 line
  display
;

: vreading ( pin -- value )  \ take an averaged ADC reading
  0  100 0 do over adc + loop  100 / nip ;

 200 variable low-temp   680 variable low-pt100   \ low temp calibration
2600 variable high-temp 1500 variable high-pt100  \ high temp calibration

: read-pt100 ( -- u )  \ read out PT100 sensor
  TEMP-POWER ios!  1 ms  TEMP-SENSE vreading  TEMP-POWER ioc! ;

: temp ( -- u )  \ read PT100 sensor temperature
  read-pt100
  \ convert sensor reading to approximate tenths of degrees C
  \ use linear relationship for now: t = (sens-lp)*(ht-lt)/(hp-lp)+lt
  low-pt100 @ -
  high-temp @ low-temp @ - *
  high-pt100 @ low-pt100 @ - /
  low-temp @ + ;

\ power and heater dividers are 100kΩ/10kΩ, so 12V = 1350 and 24V = 2700 on ADC
: sense-to-volt ( pin -- u )  \ read and convert ADC value to Volt (rounded)
  ( dup adc drop 1 ms ) vreading  24 *  1350 +  2700 / ;
: power-volt ( -- u ) POWER-SENSE sense-to-volt ;
: heater-volt ( -- u ) HEATER-SENSE sense-to-volt ;

: power? ( -- f )  \ true if the heater's power supply is present
  power-volt 6 > ;
: power-ok? ( -- f )  \ true if the power supply is between 11 and 25 Volt
  power-volt dup 11 >= swap 25 <= and ;

: heater? ( -- f ) \ true if the heater has been detected
  \ easy to detect if power is present, else power will be around 3.7V from
  \ being back-fed through the 7805 and zener - this can still detect a heater
  \ deal with both cases: pwm currently on or off
  heater-volt power-volt
  \ connected if within 1V of either the power voltage or ground
  over - 1 <=  swap 1 <= or ;

: heater-off ( -- ) 0 HEATER pwm ;
: black? ( -- f ) BLACK-BTN io@ 0= ;
: white? ( -- f ) WHITE-BTN io@ 0= ;

: setup ( -- )
  lcd-init splash
  100 HEATER +pwm  heater-off
  +adc
  imode-adc HEATER-SENSE io-mode!  HEATER-SENSE ioc!
   imode-adc POWER-SENSE io-mode!   POWER-SENSE ioc!
    imode-adc TEMP-SENSE io-mode!    TEMP-SENSE ioc!
     omode-pp TEMP-POWER io-mode!    TEMP-POWER ioc!
    imode-PULL BLACK-BTN io-mode!     BLACK-BTN ios!
    imode-PULL WHITE-BTN io-mode!     WHITE-BTN ios!
;

setup

\ -----------------------------------------------------------------------------
\ vim: set ft=forth :
